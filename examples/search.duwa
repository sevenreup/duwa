// Linear Search
ndondomeko linearSearch(arr, x) {
    za(nambala i = 0; i < arr.length(); i++) {
        ngati (arr[i] == x) {
            bweza i;
        }
    }
    bweza -1;
}

ndondomeko doLinearSearch() {
    nambala[] arr = [2, 3, 4, 10, 40];
    nambala x = 10;

    nambala result = linearSearch(arr, x);
    ngati (result == -1) { 
        lembanzr("Linear Search: Element is not present in array");
    } kapena {
        lembanzr("Linear Search: Element at index "+ result);
    }
}

doLinearSearch();

// Binary Search

ndondomeko binarySearch(arr,x) {
    nambala l = 0;
    nambala right = arr.length() - 1;
    pamene (l <= right) {
        nambala mid = l + (right - l) / 2;
        ngati (arr[mid] == x) {
            bweza mid;
        }
        ngati (arr[mid] < x) {
            l = mid + 1;
        } kapena {
            right = mid - 1;
        }
    }
    bweza -1;
}

ndondomeko doBinarySearch() {
    nambala[] arr = [2, 3, 4, 10, 40];
    nambala x = 10;

    nambala result = binarySearch(arr, x);
    ngati (result == -1) {
        lembanzr("Binary Search: Element is not present in array");
    } kapena {
        lembanzr("Binary Search: Element at index "+ result);
    }
}

doBinarySearch();

// Jump Search

ndondomeko jumpSearch(arr, x) {
    nambala n = arr.length();
    nambala step = Masamu.sqrt(n);
    nambala prev = 0;
    pamene (arr[Masamu.yochepa(step, n) - 1] < x) {
        prev = step;
        step += Masamu.sqrt(n);
        ngati (prev >= n) {
            bweza -1;
        }
    }
    pamene (arr[prev] < x) {
        prev++;
        ngati (prev == Masamu.yochepa(step, n)) {
            bweza -1;
        }
    }
    ngati (arr[prev] == x) {
        bweza prev;
    }
    bweza -1;
}

ndondomeko doJumpSearch() {
    nambala[] arr = [ 0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, 144, 233, 377, 610];
    nambala x = 55;

    nambala result = jumpSearch(arr, x);
    ngati (result == -1) {
        lembanzr("Jump Search: Element is not present in array");
    } kapena {
        lembanzr("Jump Search: Element at index "+ result);
    }
}

doJumpSearch();

// Interpolation Search

ndondomeko interpolationSearch(arr, lo, hi, x) {
    nambala pos = 0;
    // Since array is sorted, an element present
    // in array must be in range defined by corner
    ngati (lo <= hi && x >= arr[lo] && x <= arr[hi]) {
        // Probing the position with keeping
        // uniform distribution in mind.
        pos = lo + (((hi - lo) / (arr[hi] - arr[lo])) * (x - arr[lo]));
 
        // Condition of target found
        ngati (arr[pos] == x){
            bweza pos;
        }

        // If x is larger, x is in right sub array
        ngati (arr[pos] < x) {
            bweza interpolationSearch(arr, pos + 1, hi, x);
        }
 
        // If x is smaller, x is in left sub array
        ngati (arr[pos] > x) {
            bweza interpolationSearch(arr, lo, pos - 1, x);
        }
    }
    bweza -1;
}

ndondomeko doInterpolationSearch()
{
    // Array of items on which search will
    // be conducted.
    nambala[] arr = [10, 12, 13, 16, 18, 19, 20, 21, 22, 23, 24, 33, 35, 42, 47];
    nambala n = arr.length();
 
    nambala x = 18; // Element to be searched
    nambala index = interpolationSearch(arr, 0, n - 1, x);
    
    // If element was found
    ngati (index != -1) {
        lembanzr("Interpolation Search: Element found at index" + index);
    } kapena {
        lembanzr("Interpolation Search: Element not found.");
    }
}

doInterpolationSearch();

// Exponential Search

ndondomeko exponentialSearch(arr, n, x) {
    // If x is present at first location itself
    ngati (arr[0] == x) {
        bweza 0;
    }
 
    // Find range for binary search by
    // repeated doubling
    nambala i = 1;
    pamene (i < n && arr[i] <= x) {
        i = i * 2;
    }
 
    // Call binary search for the found range.
    bweza binarySearch(arr, i / 2, Masamu.yochepa(i, n), x);
}

ndondomeko doExponentialSearch() {
    nambala[] arr = [2, 3, 4, 10, 40];
    nambala x = 10;
    nambala n = arr.length();
    nambala result = exponentialSearch(arr, n, x);
    ngati (result == -1) {
        lembanzr("Exponential Search: Element is not present in array");
    } kapena {
        lembanzr("Exponential Search: Element at index "+ result);
    }
}

doExponentialSearch();

// Fibonacci Search

ndondomeko fibonacciSearch(arr, x, n) {
    // Initialize fibonacci numbers
    nambala fibMMm2 = 0; // (m-2)'th Fibonacci No.
    nambala fibMMm1 = 1; // (m-1)'th Fibonacci No.
    nambala fibM = fibMMm2 + fibMMm1; // m'th Fibonacci
 
    // fibM is going to store the smallest
    // Fibonacci Number greater than or equal to n
    pamene (fibM < n) {
        fibMMm2 = fibMMm1;
        fibMMm1 = fibM;
        fibM = fibMMm2 + fibMMm1;
    }
 
    // Marks the eliminated range from front
    nambala offset = -1;
 
    // while there are elements to be inspected.
    // Note that we compare arr[fibMm2] with x.
    // When fibM becomes 1, fibMm2 becomes 0
    pamene (fibM > 1) {
        // Check if fibMm2 is a valid location
        nambala i = Masamu.yochepa(offset + fibMMm2, n - 1);
 
        // If x is greater than the value at
        // index fibMm2, cut the subarray array
        // from offset to i
        ngati (arr[i] < x) {
            fibM = fibMMm1;
            fibMMm1 = fibMMm2;
            fibMMm2 = fibM - fibMMm1;
            offset = i;
        }
 
        // If x is greater than the value at
        // index fibMm2, cut the subarray
        // after i+1
        ngati (arr[i] > x) {
            fibM = fibMMm2;
            fibMMm1 = fibMMm1 - fibMMm2;
            fibMMm2 = fibM - fibMMm1;
        }
 
        // element found. return index
        ngati (arr[i] == x) {
            bweza i;
        }
    }
 
    // comparing the last element with x
    ngati (fibMMm1 && arr[offset + 1] == x) {
        bweza offset + 1;
    }

    bweza -1;
}

ndondomeko doFibonacciSearch() {
    nambala[] arr = [10, 22, 35, 40, 45, 50, 80, 82, 85, 90, 100,235];
    nambala x = 235;
    nambala n = arr.length();
    nambala result = fibonacciSearch(arr, x, n);
    ngati (result == -1) {
        lembanzr("Fibonacci Search: Element is not present in array");
    } kapena {
        lembanzr("Fibonacci Search: Element found at index "+ result);
    }
}

doFibonacciSearch();